#!/usr/bin/env zsh

dir=$(dirname $0)

source "$dir"/config

if pgrep -F "$panel_pid_file" > /dev/null 2>&1; then
  echo 'The panel is already running.' >&2
  exit 1
fi

trap 'trap - TERM; : >! "$panel_pid_file"; kill 0' INT TERM QUIT EXIT
trap ': >! "$panel_pid_file"; pkill -P $$; exec "$(readlink -f "$0")"' USR1

[ -e "$panel_fifo" ] && rm -f "$panel_fifo"
mkfifo "$panel_fifo"

# bspwm desktop events
bspc subscribe desktop_focus | while read line; do
  echo "W$(bspc query -T -d "$(echo $line | cut -d' ' -f3)" | jq -r '.name')"
done > "$panel_fifo" &

# music status
# Mopidy's MPD interface doesn't support channels, so we can't use `mpc subscribe`
# to listen for events. The next best thing is using `mpc idleloop` and query for
# the status, which is much better than polling.
mpc idleloop player | while read _; do
  echo "M$(mpc status 2> /dev/null | tr '\n' '|')"
done > "$panel_fifo" &

gpmdp_playback=~/.config/Google\ Play\ Music\ Desktop\ Player/json_store/playback.json
inotifywait -q -e close_write -m --format '%w' "$gpmdp_playback" | while read -r file; do
  echo "G$(cat $file | jq -c 'del(.songLyrics)')"
done > "$panel_fifo" &

# active window
while :; do
  echo "A$(bspc query -T -n focused | jq -r '.client.className' | sed -s 's/-/ /g')"
  sleep 0.1
done > "$panel_fifo" &

# time
while :; do
  echo "C$(date +'%a %_I:%M%p')"
  sleep 1
done > "$panel_fifo" &

# volume
while :; do
  echo "V$(amixer get Master | grep 'Mono: Playback')"
  sleep 0.1
done > "$panel_fifo" &

# user
while :; do
  echo "U$(whoami)"
  sleep 10
done > "$panel_fifo" &

# available updates
while :; do
  echo "P$(checkupdates | wc -l)"
  sleep 1h
done > "$panel_fifo" &

# hide bar on fullscreen events
bspc subscribe node_state | while read _ _ _ wid name state; do
  [ -z "$panel_id" ] && local panel_id=$(xdo id -a "$panel_name")

  if [ "$name" = 'fullscreen' ] && [ "$state" = 'on' ]; then
    xdo below -t "$wid" "$panel_id"
  fi
done &

"$dir"/parser < "$panel_fifo" | lemonbar \
    -d \
    -n "$panel_name" \
    -g "$geometry" \
    -o "$font_offset" \
    -f "$font1" \
    -o "$font_offset" \
    -f "$font2" \
    -o "$font_offset" \
    -f "$icon1" \
    -o 0 \
    -f "$icon2" \
    -u "$border" \
    -B "$bg_color" \
    -F "$fg_color" \
  | zsh &

# store PID for kill command
echo $$ > "$panel_pid_file"

wait

