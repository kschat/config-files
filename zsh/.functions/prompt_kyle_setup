# Load dependencies.
pmodload 'helper'

function prompt_kyle_precmd {
  setopt LOCAL_OPTIONS
  unsetopt XTRACE KSH_ARRAYS

  # get Git repository information
  if (( $+functions[git-info] )); then
    git-info
  fi
}

function prompt_kyle_setup {
  setopt LOCAL_OPTIONS
  unsetopt XTRACE KSH_ARRAYS
  prompt_opts=(cr percent subst)

  # Load required functions.
  autoload -Uz add-zsh-hook

  # Add hook for calling git-info before each command.
  add-zsh-hook precmd prompt_kyle_precmd

  hname=" at %F{blue}%m%f"


  # required to use editor mode
  zstyle ':prezto:module:prompt' managed 'yes'

  # editor mode info
  zstyle ':prezto:module:editor:info:keymap:primary' format '%F{white}——%f'
  zstyle ':prezto:module:editor:info:keymap:alternate' format '%F{cyan}——%f'

  # git info
  zstyle ':prezto:module:git:info' verbose 'yes'
  zstyle ':prezto:module:git:info:branch' format '%F{cyan}%b%f'
  zstyle ':prezto:module:git:info:clean' format ' %F{green}✔%f'
  zstyle ':prezto:module:git:info:dirty' format ' %F{red}✗%f'
  zstyle ':prezto:module:git:info:stashed' format ' %F{magenta}✚%f'
  zstyle ':prezto:module:git:info:untracked' format ' %F{yellow}●%f'
  zstyle ':prezto:module:git:info:keys' format \
    'prompt' 'on %b%C%D%S%u' \
    'rprompt' ''

  PROMPT=$'\n %F{cyan}%n%f${hname} in %F{green}%~%f ${(e)git_info[prompt]} \n ${(e)editor_info[keymap]} %f'
}

prompt_kyle_setup "$@"
